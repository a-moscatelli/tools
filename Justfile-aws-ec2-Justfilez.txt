

# this is the level-zero Justfile - see https://github.com/casey/just and https://just.systems/man/en/

# to install just:
# sudo apt update
# sudo apt install -y just

# to download a copy of this file from github:
# JU=https://raw.githubusercontent.com/a-moscatelli/tools/refs/heads/main/Justfile-aws-ec2-Justfilez.txt
# curl -o Justfile $JU
###



import? 'adhoc.Justfile'
# ... import but do not fail if not there

#host := `uname -i -o`


alias ddown := ddn
alias hi := hello

default:
  just --list

hello:
  @echo "This is an {{arch()}} machine".
#  echo "Hello, World! from a {{host}} host"


# pre-install: run apt update
install_pre:
   sudo apt update

install_docker_docker: install_pre
   sudo apt install -y docker.io

install_docker_dockercompose: install_docker_docker
   sudo apt install -y docker-compose

# install docker works - usually the first command I run on a brand new EC2 instance
install_docker: install_docker_dockercompose 


# start docker-compose
dup:
   sudo docker-compose up -d

# show docker containers
dps:
   sudo docker ps

# stop docker-compose
ddn:
   sudo docker-compose down

# docker ps - exited containers included - then, e.g., sudo docker logs (-f) <cont_name>
dpsa:
   sudo docker ps -a
   #echo you could now run: sudo docker logs -f <cont_name>

awsec2_my_public_ip:
   curl http://169.254.169.254/latest/meta-data/public-ipv4

awscli_mybuckets:
  sudo docker run --rm \
  -v ~/.aws:/root/.aws \
  -v ~/local_aws:/aws \
  amazon/aws-cli s3 ls

# example: just awscli "s3 ls am1misc | grep amwiki
awscli args: 
  sudo docker run --rm \
    -v ~/.aws:/root/.aws \
    -v ~/local_aws:/aws \
    amazon/aws-cli {{args}}

awscli_mybuckets_env argkid argksecr:
  docker run --rm -it \
    -e AWS_ACCESS_KEY_ID={{argkid}} \
    -e AWS_SECRET_ACCESS_KEY={{argksecr}} \
    -e AWS_DEFAULT_REGION=eu-central-1 \
    amazon/aws-cli s3 ls
